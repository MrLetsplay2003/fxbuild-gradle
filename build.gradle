plugins {
	id 'java-gradle-plugin'
	id 'maven-publish'
	id 'eclipse'
}

group 'me.mrletsplay'
version '1.0'

ext {
	fxloaderVersion = "1.1"
}

repositories {
	mavenCentral()
	mavenLocal()
	maven {
		url "https://maven.graphite-official.com"
	}
}

dependencies {
	implementation "me.mrletsplay:FXLoader:${fxloaderVersion}"
}

gradlePlugin {
	plugins {
		fxbuild {
			id = 'me.mrletsplay.fxbuild'
			implementationClass = 'me.mrletsplay.fxbuild.FXBuildGradlePlugin'
		}
	}
}

task generateSources(type: Copy) {
	from 'src/generated/java'
	into "${buildDir}/generated-src"
	filter { line -> line.replace("{FXLOADER_VERSION}", "${fxloaderVersion}") }
}

sourceSets {
	main {
		java {
			srcDir "${buildDir}/generated-src"
		}
	}
}

compileJava.source "${buildDir}/generated-src"
compileJava.dependsOn generateSources
